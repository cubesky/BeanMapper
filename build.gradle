buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'maven'
    id "maven-publish"
    id 'signing'
}

group 'party.liyin'
version '1.0.3'

repositories {
    mavenCentral()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/" + System.getenv("GITHUB_REPOSITORY"))
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            artifactId 'beanmapper'
            from components.java
        }
    }
}


signing {
    required {
        gradle.taskGraph.hasTask(uploadArchives)
    }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: System.getenv("OSSRH_USERNAME"), password: System.getenv("OSSRH_PASSWORD"))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: System.getenv("SONATYPE_USERNAME"), password: System.getenv("SONATYPE_PASSWORD"))
            }

            pom.project {
                name project.name
                packaging 'jar'
                description 'A faster BeanUtils.copyProperties replacement'
                url 'https://github.com/cubesky/BeanMapper'

                scm {
                    url 'https://github.com/cubesky/BeanMapper'
                    connection 'https://github.com/cubesky/BeanMapper.git'
                    developerConnection 'git@github.com:cubesky/BeanMapper.git'
                }

                licenses {
                    license {
                        name 'MIT Licence'
                        url 'https://raw.githubusercontent.com/cubesky/BeanMapper/master/LICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'cubesky'
                        name = 'CubeSky'
                        email = 'edge4chaos+sonatype@gmail.com'
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

dependencies {
    implementation group: 'org.javassist', name: 'javassist', version: '3.27.0-GA'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot', version: '2.4.2'
    testImplementation group: 'org.openjdk.jmh', name: 'jmh-core', version: '1.27'
    testImplementation group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: '1.27'
}
